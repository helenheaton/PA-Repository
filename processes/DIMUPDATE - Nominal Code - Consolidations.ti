#region Prolog

sDim = 'Nominal Code';
sSubset = GetProcessName;
nTemp = 1;

# Unwind consolidation
sMDX = 'Hierarchize(TM1SubsetAll(['| sDim |']))';

SubsetDestroy( sDim, sSubset );
SubsetCreate( sDim, sSubset, nTemp );
SubsetMDXSet( sDim, sSubset, sMDX );
SubsetMDXSet (sDim, sSubset, '');

nSubset = SubsetGetSize( sDim, sSubset );
nCount = nSubset; 
While(nCount >= 1);
  sEl = SubsetGetElementName( sDim, sSubset, nCount );
  nPar = ELPARN( sDim, sEl );
  nCount2 = 1; 
  While (nCount2 <= nPar);
    sPar = ELPAR( sDim, sEl, nCount2 );
    DimensionElementComponentDelete( sDim, sPar, sEl );
    nCount2 = nCount2 +1;
  End;
nCount = nCount-1;
End;

DatasourceDimensionSubset = sSubset;
DatasourceNameForClient = sDim; 
DatasourceNameForServer = sDim; 
DatasourceType = 'SUBSET';
#endregion
#region Metadata

sPar = AttrS(sDim, vCode, 'PL Summary Allocation');
nWeight = AttrN(sDim, vCode, 'Weight');

IF(sPar@='');
  Itemskip;
Endif;

If(nWeight=0);
  nWeight=1;
  AttrPutN( nWeight, sDim, vCode, 'Weight');
Endif;


DimensionElementComponentAdd( sDim, sPar, vCode, nWeight );
#endregion