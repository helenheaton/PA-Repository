#region Prolog

sDimDept = 'Department';
sDimEntity = 'Entity';
sDimContract = 'Contract';
sSubset = GetProcessName;
nTemp = 1;
 

DimensionElementInsert( sDimDept, '','All Departments', 'C' );
DimensionElementInsert( sDimEntity, '','All Entities', 'C' );
DimensionElementInsert( sDimContract, '','All Contracts', 'C' );
DimensionElementInsert( sDimContract, '','Camden', 'C' );
DimensionElementInsert( sDimContract, '','Powys', 'C' );


# Unwind consolidation
sMDX = 'TM1Subsetall([' | sDimDept | '])';
SubsetDestroy( sDimDept, sSubset );
SubsetCreate( sDimDept, sSubset, nTemp );
SubsetMDXSet( sDimDept, sSubset, sMDX );
SubsetMDXSet (sDimDept, sSubset, '');
nSubset = SubsetGetSize( sDimDept, sSubset );
nCount = nSubset; 
While(nCount >= 1);
  sEl = SubsetGetElementName( sDimDept, sSubset, nCount );
  nPar = ELPARN( sDimDept, sEl );
  nCount2 = 1; 
  While (nCount2 <= nPar);
    sPar = ELPAR( sDimDept, sEl, nCount2 );
    DimensionElementComponentDelete( sDimDept, sPar, sEl );
    nCount2 = nCount2 +1;
  End;
nCount = nCount-1;
End;

# Unwind consolidation
sMDX = 'TM1Subsetall([' | sDimEntity | '])';
SubsetDestroy( sDimEntity, sSubset );
SubsetCreate( sDimEntity, sSubset, nTemp );
SubsetMDXSet( sDimEntity, sSubset, sMDX );
SubsetMDXSet (sDimEntity, sSubset, '');
nSubset = SubsetGetSize( sDimEntity, sSubset );
nCount = nSubset; 
While(nCount >= 1);
  sEl = SubsetGetElementName( sDimEntity, sSubset, nCount );
  nPar = ELPARN( sDimEntity, sEl );
  nCount2 = 1; 
  While (nCount2 <= nPar);
    sPar = ELPAR( sDimEntity, sEl, nCount2 );
    DimensionElementComponentDelete( sDimEntity, sPar, sEl );
    nCount2 = nCount2 +1;
  End;
nCount = nCount-1;
End;

# Unwind consolidation
sMDX = 'TM1Subsetall([' | sDimContract | '])';
SubsetDestroy( sDimContract, sSubset );
SubsetCreate( sDimContract, sSubset, nTemp );
SubsetMDXSet( sDimContract, sSubset, sMDX );
SubsetMDXSet (sDimContract, sSubset, '');
nSubset = SubsetGetSize( sDimContract, sSubset );
nCount = nSubset; 
While(nCount >= 1);
  sEl = SubsetGetElementName( sDimContract, sSubset, nCount );
  nPar = ELPARN( sDimContract, sEl );
  nCount2 = 1; 
  While (nCount2 <= nPar);
    sPar = ELPAR( sDimContract, sEl, nCount2 );
    DimensionElementComponentDelete( sDimContract, sPar, sEl );
    nCount2 = nCount2 +1;
  End;
nCount = nCount-1;
End;


DimensionElementComponentAdd( sDimContract, 'All Contracts', 'Camden', 1 );
DimensionElementComponentAdd( sDimContract, 'All Contracts', 'Powys', 1 );


#endregion
#region Metadata

IF(Dimix(sDimDept, vCode)=0);
  DimensionElementInsert( sDimDept, '',vCode, 'N' );
Endif;
DimensionElementComponentAdd( sDimDept, 'All Departments', vCode, 1 );

IF(Dimix(sDimEntity, vCoName)=0);
  DimensionElementInsert( sDimEntity, '',vCoName, 'N' );
Endif;
DimensionElementComponentAdd( sDimEntity, 'All Entities', vCoName, 1 );


IF(vContract@='N/A');
  vContract='None';
Endif;

IF(Dimix(sDimContract, vContract)=0);
  DimensionElementInsert( sDimContract, '',vContract, 'N' );
Endif;
IF(Scan('Camden',vContract)<>0);
  DimensionElementComponentAdd( sDimContract, 'Camden', vContract, 1 );
Elseif(Scan('Powys',vContract)<>0);
  DimensionElementComponentAdd( sDimContract, 'Powys', vContract, 1 );
Else;
  DimensionElementComponentAdd( sDimContract, 'All Contracts', vContract, 1 );
Endif;
#endregion
#region Data

AttrPutS( vName, sDimDept, vCode, 'Department Name');
AttrPutS( vRegion, sDimDept, vCode, 'Region');
AttrPutS( vType, sDimDept, vCode, 'Department Type');
AttrPutS( vCoCode, sDimDept, vCode, 'Company Code');
AttrPutS( vCoName, sDimDept, vCode, 'Company Name');
AttrPutS( vLegal, sDimDept, vCode, 'Legal Entity');
AttrPutS( vContract, sDimDept, vCode, 'Contract');
AttrPutS( vCode | ' - ' | vName, sDimDept, vCode, 'Alias');
AttrPutS( vMapping, sDimDept, vCode, 'Mapping');

IF(vHistoric@<>'');
  AttrPutN( 1, sDimDept, vCode, 'Historic Department');
Endif;

AttrPutS( vCoCode, sDimEntity, vCoName, 'Company Code');
AttrPutS( vLegal, sDimEntity, vCoName, 'Legal Entity');
AttrPutS( vFileName, sDimEntity, vCoName, 'File');


# sName = Subst(vLegal, 1, Scan('Limited',vLegal)-1)|'Ltd';
# AttrPutS( sName, sDimEntity, vCoName, 'Alias');
#endregion