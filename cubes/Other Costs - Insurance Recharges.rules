  # --------------------------------------------------------------------------------------------------
  # Other Costs - Insurance Recharges
  # Maps the insurance costs to care homes and admin departments
  #
  # Paul De Duonni
  # Last updated 29/11/23
  # --------------------------------------------------------------------------------------------------
  
  Skipcheck;
  Feedstrings;
    
  # Get the insurance costs from Other Costs - Resident & Staff Related & Other Costs cube and split between "Care Homes" and "Admin Departments"  
  ['Care Homes','R201']=N:
    IF(ATTRS('Department', !Department, 'Company Name')@=!Entity,
      IF(ATTRS('Department', !Department, 'Insurance (R201)')@='Y',
        DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department, 'R201'),
      CONTINUE),   
    CONTINUE);
  
   ['Admin Departments','R201']=N:
      IF(ATTRS('Department', !Department, 'Company Name')@=!Entity,
       IF(ATTRS('Department', !Department, 'Insurance (R201)')@='',
         DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department, 'R201'),
       CONTINUE),   
      CONTINUE);


  # Map R201 from Care Homes to R201 Admin Departments (reversals for LH01 & CC01 and adjustment for NH01)
  
  ['R201 Admin (From Care Homes)', 'NH01','R201']=
  -['Admin Departments','NH01','R201'] - ['Care Homes', 'Shaw Foundation', 'All Departments','R201' ]; 
  
  ['R201 Admin (From Care Homes)','R201']=
     IF(ATTRS('Department',!Department, 'Alias')@=ATTRS('Entity',!Entity,'R201 Dept -> R201 Admin'),
       -['Care Homes','All Departments','R201'],
      CONTINUE);

  ['R201 Admin (From Care Homes)', 'LH01','R201']=
  -['Admin Departments','LH01','R201'];
  
  #['R201 Admin (From Care Homes)', 'CC01','R201']=
  #-['Admin Departments','CC01','R201'];
  

 
  # Map R201 in Group (Care Homes + Admin) to R201 in NH47
  ['NH47 (From GP01)','Group','NH47']=
    -['Total Insurance Costs','Group','All Departments'];
 
  # Map R201 in Entities to NH47 Income
  ['NH47 (Income & Costs From Entity)','NH47']=
    IF(ATTRS('Nominal Code', !Nominal Code , 'Alias')@=ATTRS('Entity',!Entity,'R201 Entity -> NH47 Income'),
      ['Total Insurance Costs','All Departments','R201'],
    CONTINUE);
  
  # Map R201 in Entities to NH47 Costs
  ['NH47 (Income & Costs From Entity)','NH47']=
    IF(ATTRS('Nominal Code', !Nominal Code , 'Alias')@=ATTRS('Entity',!Entity,'R201 Entity -> NH47 Costs'),
      ['Total Insurance Costs','All Departments','R201'],
    CONTINUE);
 
  # Map R201 in Entities to R200 in Admin Departments
  ['R200 Admin (From Entity)','R200']=
    IF(ATTRS('Department',!Department, 'Alias')@=ATTRS('Entity',!Entity,'R201 Entity -> R200 Admin'),
      ['Total Insurance Costs','All Departments','R201'],
    CONTINUE);

  
FEEDERS;

# Internal Feeders

['Care Homes','All Departments','R201']=>
DB('Other Costs - Insurance Recharges', !Version, !t_Period, !t_Year, ATTRS('Entity',!Entity,'R201 Dept -> R201 Admin'), !Entity, 'R201 Admin (From Care Homes)', 'R201');

['Total Insurance Costs','Group']=>
DB('Other Costs - Insurance Recharges', !Version, !t_Period, !t_Year, 'NH47', 'Group', 'NH47 (From GP01)', !Nominal Code);

['Total Insurance Costs','R201']=>
DB('Other Costs - Insurance Recharges', !Version, !t_Period, !t_Year, 'NH47', !Entity, 'NH47 (Income & Costs From Entity)', ATTRS('Entity',!Entity,'R201 Entity -> NH47 Income'));

['Total Insurance Costs','R201']=>
DB('Other Costs - Insurance Recharges', !Version, !t_Period, !t_Year, 'NH47', !Entity, 'NH47 (Income & Costs From Entity)', ATTRS('Entity',!Entity,'R201 Entity -> NH47 Costs'));
 
['Total Insurance Costs','R201']=>
DB('Other Costs - Insurance Recharges', !Version, !t_Period, !t_Year, ATTRS('Entity',!Entity,'R201 Entity -> R200 Admin'), !Entity, 'R200 Admin (From Entity)', 'R200');

['Total Insurance Costs','R201']=>
DB('Other Costs - Insurance Recharges', !Version, !t_Period, !t_Year, !Department, !Entity, 'R201 Admin (From Care Homes)', !Nominal Code);


# External feeders
[]=>DB('PL by Dept', !Version, !t_Period, !t_Year, !Department, Attrs('Department', !Department, 'Contract'), !'Nominal Code' );