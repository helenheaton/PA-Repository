SKIPCHECK;
FEEDSTRINGS;

# Other in year charges are not indexed. 
# Value comes from the Amount cubes. Indexing is applied in the source cube. 

#Region Opening and Closing Months

[]=N:
IF(DB('Admin - Department Control', !Department, 'Open Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) < Numbr(DB('Admin - Department Control', !Department, 'Open Year')),
    0,
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Open Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) = Numbr(DB('Admin - Department Control', !Department, 'Open Year')),
    IF(AttrN('t_Period', !t_Period, 'Index') < AttrN('t_Period',DB('Admin - Department Control', !Department, 'Open Month'), 'Index'),
      0,
      Continue),
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Close Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) > Numbr(DB('Admin - Department Control', !Department, 'Close Year')),
    0,
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Close Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) = Numbr(DB('Admin - Department Control', !Department, 'Close Year')),
    IF(AttrN('t_Period', !t_Period, 'Index') > AttrN('t_Period',DB('Admin - Department Control', !Department, 'Close Month'), 'Index'),
      0,
      Continue),
    Continue));

#EndRegion




#####
# CARE
['Value', 'Care AEI']=N:
IF(AttrN('Contract',!Contract, 'Utility Recharge Flag')<>0 % AttrN('Contract',!Contract, 'Supplementary Care Income Recharge Flag')<>0,
  DB('Subcontract - Other In Year Charges', !Version, !t_Period, !t_Year, !Department, !Contract, !'Cost Type', 'Value' )
  ,0)
+
DB('Subcontract - Fixed Base Amount', !Version, !t_Year, !Department, !Contract, !'Cost Type', 'Calculated Value' )
*
# Depending on the method, depends on how the value is spread between periods
# Feb is in the next year, so need to look at year+1 for the leap year
IF(ATTRS( 'Contract', !Contract, 'Care Spread Method')@='Days',
  IF(MOD( Numbr(Attrs('t_Year',!t_Year,'Financial Year'))+1,  4 )=0,
  Attrn('t_Period',!t_Period,'Number of Days Leap') \ Attrn('t_Period','FY','Number of Days'),
  Attrn('t_Period',!t_Period,'Number of Days') \ Attrn('t_Period','FY','Number of Days')),
1\12)
+
DB('Subcontract - Variable Base Amount', !Version,
IF(AttrN('t_Period',!t_Period,'Index')>6,  'H2',   'H1'),
Attrs('t_Year', !t_Year, 'Financial Year'), !Department, !Contract, 'Calculated Value' )
*
# Depending on the method, depends on how the value is spread between periods
IF(ATTRS( 'Contract', !Contract, 'Care Spread Method')@='Days',
  IF(MOD( Numbr(Attrs('t_Year', !t_Year, 'Financial Year'))+1,  4 )=0,
  Attrn('t_Period',!t_Period,'Number of Days Leap') \ Attrn('t_Period',
    IF(AttrN('t_Period',!t_Period,'Index')>6,  'H2',   'H1'),'Number of Days'),
  Attrn('t_Period',!t_Period,'Number of Days') \ Attrn('t_Period',
    IF(AttrN('t_Period',!t_Period,'Index')>6,  'H2',   'H1'),'Number of Days')),
1\6)
;

#####
# Care FPI
['Value', 'Care RPI']=N:
IF(AttrN('Contract',!Contract, 'Utility Recharge Flag')<>0 % AttrN('Contract',!Contract, 'Supplementary Care Income Recharge Flag')<>0,
  DB('Subcontract - Other In Year Charges', !Version, !t_Period, !t_Year, !Department, !Contract, !'Cost Type', 'Value' )
  ,0)
+
DB('Subcontract - Fixed Base Amount', !Version, !t_Year, !Department, !Contract, !'Cost Type', 'Calculated Value' )
*
# Depending on the method, depends on how the value is spread between periods
IF(ATTRS( 'Contract', !Contract, 'Managed Spread Method')@='Days',
  IF(MOD( Numbr(Attrs('t_Year', !t_Year, 'Financial Year'))+1,  4 )=0,
  Attrn('t_Period',!t_Period,'Number of Days Leap') \ Attrn('t_Period','FY','Number of Days'),
  Attrn('t_Period',!t_Period,'Number of Days') \ Attrn('t_Period','FY','Number of Days')),
1\12);

#####
# MANAGED
['Value', 'Managed']=N:
IF(AttrN('Contract',!Contract, 'Utility Recharge Flag')<>0 % AttrN('Contract',!Contract, 'Supplementary Care Income Recharge Flag')<>0,
  DB('Subcontract - Other In Year Charges', !Version, !t_Period, !t_Year, !Department, !Contract, !'Cost Type', 'Value' )
  ,0)
+
DB('Subcontract - Fixed Base Amount', !Version, !t_Year, !Department, !Contract, !'Cost Type', 'Calculated Value' )
*
# Depending on the method, depends on how the value is spread between periods
IF(ATTRS( 'Contract', !Contract, 'Managed Spread Method')@='Days',
  IF(MOD( Numbr(Attrs('t_Year', !t_Year, 'Financial Year'))+1,  4 )=0,
  Attrn('t_Period',!t_Period,'Number of Days Leap') \ Attrn('t_Period','FY','Number of Days'),
  Attrn('t_Period',!t_Period,'Number of Days') \ Attrn('t_Period','FY','Number of Days')),
1\12);


#####
# FM
['Value', {'FM AEI','FM RPI'}]=N:
IF(AttrN('Contract',!Contract, 'Utility Recharge Flag')<>0 % AttrN('Contract',!Contract, 'Supplementary Care Income Recharge Flag')<>0,
  DB('Subcontract - Other In Year Charges', !Version, !t_Period, !t_Year, !Department, !Contract, !'Cost Type', 'Value' )
  ,0)
+
DB('Subcontract - Fixed Base Amount', !Version, !t_Year, !Department, !Contract, !'Cost Type', 'Calculated Value' )
*
# Depending on the method, depends on how the value is spread between periods
IF(ATTRS( 'Contract', !Contract, 'FM Spread Method')@='Days',
  IF(MOD( Numbr(Attrs('t_Year', !t_Year, 'Financial Year'))+1,  4 )=0,
  Attrn('t_Period',!t_Period,'Number of Days Leap') \ Attrn('t_Period','FY','Number of Days'),
  Attrn('t_Period',!t_Period,'Number of Days') \ Attrn('t_Period','FY','Number of Days')),
1\12);

#####
#Irrecoverable VAT
['Value','Current Year','Irrecoverable VAT calc']=N:
['Value', 'Total for Irrecoverable VAT']*DB('Admin - Model Parameters', 'Current Year', 'VAT Rate' );

['Value','Year+1','Irrecoverable VAT calc']=N:
['Value', 'Total for Irrecoverable VAT']*DB('Admin - Model Parameters', 'Budget Year', 'VAT Rate' );
 
FEEDERS;

['Value']=>DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Contract, !Department, !Contract, !'Cost Type', 'Original Value');
['Value','Irrecoverable VAT calc','FY','All Contracts']=>DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, 'Irrecoverable Input VAT', 'Fixed Amount');