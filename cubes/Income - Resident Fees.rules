  # --------------------------------------------------------------------------------------------------
  # Income - Resident Fees
  # Calculates block, market and spot income using Income - Bed Numbers and Income - Bed Rates cubes 
  #
  # Paul De Duonni
  # Last updated 10/11/23
  # --------------------------------------------------------------------------------------------------
  
  Skipcheck;
  Feedstrings;
  
  #Average totals for full year where required
  ['FY']=C:
  IF(ATTRS('m_Resident Fees Calculation', !m_Resident Fees Calculation, 'Average')@='Y',
  (['April']+['May']+['June']+['July']+['August']+['September']+['October']+['November']+['December']+['January']+['February']+['March'])/12,
  CONTINUE);
  
  
  # Get the number of beds and hours from the Income - Bed Numbers cube
  ['Total Block Beds']=N:
      IF(ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Care Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Nursing Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Specialist Bed',
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, !Bed Types, 'Block' ),
      CONTINUE);
      
  ['Total Market Beds']=N:
      IF(ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Care Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Nursing Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Specialist Bed',
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, !Bed Types, 'Market' ),
      CONTINUE);
      
  ['Total Spot Beds']=N:
      IF(ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Care Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Nursing Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Specialist Bed',
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, !Bed Types, 'Spot' ),
      CONTINUE);
  
  #['Weekly 1:1 Hours']=N:
  #    DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, 'Supplementary Hours', 'Total Funding Types' );
  ['Weekly Block Domiciliary Care Hours','Domiciliary Care']=N:
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, 'Domiciliary Care', 'Block' );   
  ['Weekly Market Domiciliary Care Hours','Domiciliary Care']=N:
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, 'Domiciliary Care', 'Market' );
  ['Weekly Spot Domiciliary Care Hours','Domiciliary Care']=N:
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, 'Domiciliary Care', 'Spot' );   


  # Get the number of days in the period
  # If year is a leap year get 'Number of days leap'  
  ['Number of Days Per Period']=
      IF(MOD(NUMBR(ATTRS( 't_Year', !t_Year, 'Financial Year' ))+1,4)=0,
           ATTRN( 't_Period', !t_Period, 'Number of Days Leap' ),
           CONTINUE);
   
  # Otherwise just get 'Number of days'
  ['Number of Days Per Period']=N:   
      ATTRN( 't_Period', !t_Period, 'Number of Days' );      


  # Get the bed rates from the Income - Bed Rates cube
  ['Block Bed Rate']=N:
  IF(ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Care Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Nursing Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Specialist Bed',
      DB('Income - Bed Rates', !Version, !t_Year, !Department, !Bed Types, 'Block' ),
      CONTINUE);
  ['Market Bed Rate']=N:
  IF(ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Care Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Nursing Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Specialist Bed',
      DB('Income - Bed Rates', !Version, !t_Year, !Department, !Bed Types, 'Market' ),
      CONTINUE);
  ['Spot Bed Rate']=N:
  IF(ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Care Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Nursing Bed'% ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Specialist Bed',
      DB('Income - Bed Rates', !Version, !t_Year, !Department, !Bed Types, 'Spot' ),
  CONTINUE);
  
  ['1:1 Hourly Rate']=N:
      DB('Income - Bed Rates', !Version, !t_Year, !Department, !Bed Types, '1:1' );
  ['Block Domiciliary Care Hourly Rate']=N:
      DB('Income - Bed Rates', !Version, !t_Year, !Department, 'Domiciliary Care', 'Block' );
  ['Market Domiciliary Care Hourly Rate']=N:
      DB('Income - Bed Rates', !Version, !t_Year, !Department, 'Domiciliary Care', 'Market');
  ['Spot Domiciliary Care Hourly Rate']=N:
      DB('Income - Bed Rates', !Version, !t_Year, !Department, 'Domiciliary Care', 'Spot' );


  # Calculate care bed income
  ['Gross Block Income']=N:
      ['Block Bed Rate']\7 * ['Number of Days per Period'] * ['Total Block Beds']; 
  ['Gross Market Income']=N:
      ['Market Bed Rate']\7 * ['Number of Days per Period'] * ['Total Market Beds'];
  ['Gross Spot Income']=N:
      ['Spot Bed Rate']\7 * ['Number of Days per Period'] * ['Total Spot Beds'];


  # Calculate the impact of voids
  ['Number of Void Beds']=N:
      (['Total Market Beds'] + ['Total Spot Beds']) * ['Market / Spot Void %'];
  ['Adjusted Total Beds']=N:
       ['Total Number of Beds'] - ['Number of Void Beds'];
  ['Void Adjustment (Market)']=N:
      - ['Gross Market Income'] * ['Market / Spot Void %'];
  ['Void Adjustment (Spot)']=N:
      - ['Gross Spot Income'] * ['Market / Spot Void %'];
      
   ['Market / Spot Void %']=C:
   -(['Void Adjustment (Market)']  + ['Void Adjustment (Spot)']) \  (['Gross Spot Income'] + ['Gross Market Income']);
  

   
  # Calculate 1:1 income and total for the year
  ['1:1 Income']=N:
      ['Weekly 1:1 Hours']\7 * ['Number of Days Per Period'] * ['1:1 Hourly Rate'];
  
  
  # Calculate Total Care Bed Income
  ['Total Care Bed Income']=N:
      ['Net Block Income'] + ['Net Market Income'] +['Net Spot Income'] + ['1:1 Income'];  
  
  
  # Calculate domiciliary care income
  ['Gross Block Domiciliary Care Income']=N:
      ['Weekly Block Domiciliary Care Hours']\7 * ['Number of Days per Period'] * ['Block Domiciliary Care Hourly Rate']; 
  ['Gross Spot Domiciliary Care Income']=N:
      ['Weekly Spot Domiciliary Care Hours']\7 * ['Number of Days per Period'] * ['Spot Domiciliary Care Hourly Rate'];
  ['Gross Market Domiciliary Care Income']=N:
      ['Weekly Market Domiciliary Care Hours']\7 * ['Number of Days per Period'] * ['Market Domiciliary Care Hourly Rate'];
  
  
  # Get the FNC Rate from Model Parameters cube but only if Bed Type attribute is "Nursing Bed" and if FNC Calculated Separately flag = Yes  
  ['FNC Rate Per Week','Current Year']=N:
     IF(ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Nursing Bed',
      DB('Admin - Model Parameters', 'Current Year', 'FNC Rate'),
     CONTINUE);
     
   ['FNC Rate Per Week','Year+1']=N:
     IF(ATTRS('Bed Types', !Bed Types, 'Care Type' )@='Nursing Bed',
      DB('Admin - Model Parameters', 'Budget Year', 'FNC Rate'),
     CONTINUE);

  #Calculate FNC Income
  ['Gross FNC Income']=N:
     IF(DB('Income - Resident Fees', !Version, !t_Year, !t_Period, !Department, !'Bed Types', 'Calculate FNC Income Separately?' )@='Yes',
       ['FNC Rate Per Week']\7 * ['Number of Days per Period'] * ['Total Number of Beds'],
     CONTINUE);
  
  ['FNC Void Adjustment']=N:
      - ['Gross FNC Income'] * ['FNC Void %'];
  
  
  # Calculate Total Income and total for the year
  ['Total Income']=N:
      ['Total Care Bed Income'] + ['Total Domiciliary Care Income'] + ['Net FNC Income']; 
   
#################################################################################################################
FEEDERS;
#################################################################################################################

### Internal Feeders ###
['Total Number of Beds']=>['Number of Void Beds'],['Adjusted Total Beds'],['Net Block Income'],['Net Market Income'],
                          ['Net Spot Income'],['Total Care Bed Income'],['Net FNC Income'],['Total Income'];

['Weekly 1:1 Hours']=>['1:1 Income'];
['Total Number of Domiciliary Care Hours','Domiciliary Care']=>['Total Domiciliary Care Income'];
['Total Number of Beds']=>['Number of Days Per Period'];



### External Feeders ###
# Added by Helen 07/12/2023
['All Bed Types'] => DB('Income - Resident Fees - Summary', !Version, !t_Year, !t_Period, !Department, !m_Resident Fees Calculation, ATTRS( 'm_Resident Fees Calculation', !m_Resident Fees Calculation, 'Nominal Code'));

