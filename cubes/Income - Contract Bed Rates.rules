 # --------------------------------------------------------------------------------------------------
  # Contract Bed Rates
  # Calculates the indexed bed rates for major contracts
  #
  # Written by Jenny Hassett 
  # --------------------------------------------------------------------------------------------------
  
SKIPCHECK;
FEEDSTRINGS;
#If year > base year, copy 'Base Bed Rate' from the prior year
#Note for de Montfort, we need to use 2004 not Base Year

['Base Bed Rate']=N:
#Note for de Montfort, we need to use 2004 not Base Year
If((!Contract)@='De Montfort' & (!t_Financial Year)@>'2004',
DB('Income - Contract Bed Rates', !Version, ATTRS('t_Financial Year', !t_Financial Year, 'Year-1'), !Contract, !Bed Types, 'Base Bed Rate' ),
If((!t_financial year)@>ATTRS('Contract',!Contract,'Base Year'),
DB('Income - Contract Bed Rates', !Version, ATTRS('t_Financial Year', !t_Financial Year, 'Year-1'), !Contract, !Bed Types, 'Base Bed Rate' )
,STET));

#Get the Prior Year Bed Rate
#Note for de Montfort, we need to use 2004 not Base Year
['Prior Year Bed Rate']=N:
If((!Contract)@='De Montfort' & (!t_Financial Year)@>'2004',
DB('Income - Contract Bed Rates', !Version, ATTRS('t_Financial Year', !t_Financial Year, 'Year-1'), !Contract, !Bed Types, 'Uplifted Bed Rate' ),
If((!t_financial year)@>ATTRS('Contract',!Contract,'Base Year'),
DB('Income - Contract Bed Rates', !Version, ATTRS('t_Financial Year', !t_Financial Year, 'Year-1'), !Contract, !Bed Types, 'Uplifted Bed Rate' )
,STET));

#Get the relevant uplift % dependant on contract
['Uplift %']=N:
   DB('Admin - Index Rates', !'t_Financial Year', !Contract, (DB('Income - Contract Bed Rates', !Version, !'t_Financial Year', !Contract, !'Bed Types', 'Uplift Type' )) );

#Over-ride Uplift % is manual entry if relevant

#Calculate Uplifted Bed Rate based on the contract uplift
['Uplifted Bed Rate']=C:
0;

#If year > base year, copy 'Uplift Type' from the prior year
['Uplift Type']=S:
If((!t_Financial Year)@>ATTRS('Contract',!Contract,'Base Year'),
DB('Income - Contract Bed Rates', !Version, ATTRS('t_Financial Year', !t_Financial Year, 'Year-1'), !Contract, !'Bed Types', 'uplift type' )
,STET);


#If Uplift type is Year on year (Hereford Contract), then use 'Prior Year Bed Rate' & If there is an override uplift then use it, if not use original uplift %
['Uplifted Bed Rate']=N:
    If(DB( 'Income - Contract Bed Rates', !Version, !'t_Financial Year', !Contract, !'Bed Types', 'Uplift Type' ) @='Mixed Year on Year' 
    & db('Income - Contract Bed Rates', !Version, !'t_Financial Year', !Contract, !'Bed Types', 'Over-ride Uplift %' )<>0,
        ['Prior Year Bed Rate']*['Over-ride Uplift %'],
CONTINUE);

['Uplifted Bed Rate']=N:         
    If(db('Income - Contract Bed Rates', !Version, !'t_Financial Year', !Contract, !'Bed Types', 'Over-ride Uplift %' )<>0,
        ['Base Bed Rate']*['Over-ride Uplift %'],    
CONTINUE);

['Uplifted Bed Rate']=N:         
    If(DB( 'Income - Contract Bed Rates', !Version, !'t_Financial Year', !Contract, !'Bed Types', 'Uplift Type')  @='Mixed Year on Year',
     ['Prior Year Bed Rate']*['Uplift %'],
   
     ['Base Bed Rate']*['Uplift %']);     
   
FEEDERS;
# Internal Feeders
['Base Bed Rate']=>['Uplifted Bed Rate'];
['Base Bed Rate']=>['Uplift %'];


# External Feeders
['Uplifted Bed Rate']=>DB('Income - Unitary Charge Income', !Version, !t_Financial Year, 'FY', 'All Departments', !'Bed Types', 'Bed Rate excl FNC' );

['Uplifted Bed Rate']=>DB('Income - Unitary Charge Income', !Version, !t_Financial Year, 'FY', 'All Departments', 'Supplementary Hours', 'Hourly Rate');
