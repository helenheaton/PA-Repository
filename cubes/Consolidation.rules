SKIPCHECK;
FEEDSTRINGS;

['% Change on Prior Year']=C:  (['Year+1']-['Current Year'])\['Current Year'];

# Forecast is Actual until current month, then it takes the version specified in the model parameter cube
['Forecast','Current Year']=N:
  IF(AttrN('t_Period', !t_Period, 'Index')<=AttrN('t_Period', DB('Admin - Model Parameters', 'Actual Period', 'Value'), 'Index'),
    ['Actual'],
    DB('Consolidation', DB('Admin - Model Parameters', 'Version for Forecast', 'Value'), !t_Period, !t_Year, !Entity, !Consol Layer, !'Nominal Code' )
    );
    
    
#############
#Region Exclusions

# Shaw foundation is not included in the consolidation
['Shaw Foundation']=N:0;

#EndRegion
#############


# Bring source from PL by Entity
['Source']=N:
DB('PL by Entity', !Version, !t_Period, !t_Year, !Entity, 'All Departments', 'All Contracts', !'Nominal Code' );


##########
#Region Eliminations

# Internal Contract income
['Eliminations',{'P200','P201'}]=N:
-['Source'];

# Property income
# = Sum of entities for this line less P230 for all entities (should only be FM Services).
['Eliminations','P230']=N:
-['Source'];

# Property and building costs
# = Sum of entities for this line less P230 for all entities (from above) less difference between P201 (FM subcontract income) and R281 (FM subcontract costs).
['Eliminations','R281']=N:
-['R281','Source']
-(['R281','Source']-['P201','Source'])
- ['P230','Source'] ;

# Care Operating costs
# = Sum of entities for this line less difference between P200 (care subcontract income) and R280 (care subcontract costs).
['Eliminations','R280']=N:
-['R280','Source']
-(['R280','Source']-['P200','Source'])
;

# Investment income
# = Sum of entities for this line less P450 (dividends) less P302 (internal loan interest receivable) for all entities
['Eliminations',{'P450','P302'}]=N: 
-['Source'];

# Sub debt interest receivable
# = Sum of entities for this line less P310, P311, P312, P313. P314, P315, P316, P350, P395, P396 for all entities
['Eliminations',{'P310','P311','P312','P313','P314','P315','P316','P350','P395','P396'}]=N:
-['Source'];

# Sub debt interest (payable)
# = Sum of entities less R255, R260 for all entities
['Eliminations',{'R255','R260'}]=N:
-['Source'];

# Other interest payable
# = Sum of entities less S213 for all entities
['Eliminations','S213']=N:
-['Source'];

# Other overheads
# = Sum of entities for this line less Internal Management fees (from income section) less Overhead Recharges (from income section).
['Eliminations','R285']=N:
-['Nominal Code':'Nominal Code':'Overhead Recharges','Source'];

['Eliminations']=N:
IF(elisanc('Nominal code', 'Overhead Recharges', !Nominal Code)<>0,
  -['Source'],
  Continue);
  
['Eliminations', 'R290']=N:
  -(['P404','Source']+['P407','Source']+['P409','Source']+['P411','Source']);

['Eliminations',{'P404','P407','P409','P411'}]=N:
-['Source'];

['Eliminations', 'R282']=N:
  -(['P420','Source']+['P421','Source']+['P422','Source']+['P423','Source']+['P426','Source']);
['Eliminations',{'P420','P421','P422','P423','P426'}]=N:
-['Source'];


#EndRegion
########


FEEDERS;

['Actual','Current Year']              =>['Forecast'];
['Board Version','Current Year']       =>['Forecast'];
['Operational  Budget','Current Year'] =>['Forecast'];
['Steady State','Current Year']        =>['Forecast'];

#Region Elimination Feeders

['Source','P200']                      =>['Eliminations'];
['Source','P201']                      =>['Eliminations'];
['Source','P230']                      =>['Eliminations'];
['Source',{'R281', 'P201', 'P230'}]    =>['Eliminations','R281'];
['Source',{'R280', 'P200'}]            =>['Eliminations','R280'];
['Source',{'P450', 'P302'}]            =>['Eliminations'];
['Source',{'P310','P311','P312','P313','P314','P315','P316','P350','P395','P396'}] =>['Eliminations'];
['Source',{'R255', 'R260'}]            =>['Eliminations'];
['Source','S213']                      =>['Eliminations'];

['Nominal Code':'Nominal Code':'Overhead Recharges','Source'] =>['R285','Eliminations'];
['Nominal Code':'Nominal Code':'Overhead Recharges','Source'] =>['Eliminations'];

['Source',{'P404', 'P407', 'P409','P411'}]                    =>['Eliminations','R290'];
['Source',{'P404', 'P407', 'P409','P411'}]                    =>['Eliminations'];

['Source',{'P420','P421','P422','P423','P426'}]               =>['Eliminations','R282'];
['Source',{'P420','P421','P422','P423','P426'}]               =>['Eliminations'];


#EndRegion
