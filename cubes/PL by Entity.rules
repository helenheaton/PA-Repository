SKIPCHECK;
FEEDSTRINGS;



['% Change on Prior Year']=C:  (['Year+1']-['Current Year'])\['Current Year'];


# Actuals and Projections are loaded by CSV
['Actual']=N:STET;
['Projections']=N:STET;



#Region Opening and Closing Months

[]=N:
IF(DB('Admin - Department Control', !Department, 'Open Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) < Numbr(DB('Admin - Department Control', !Department, 'Open Year')),
    0,
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Open Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) = Numbr(DB('Admin - Department Control', !Department, 'Open Year')),
    IF(AttrN('t_Period', !t_Period, 'Index') < AttrN('t_Period',DB('Admin - Department Control', !Department, 'Open Month'), 'Index'),
      0,
      Continue),
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Close Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) > Numbr(DB('Admin - Department Control', !Department, 'Close Year')),
    0,
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Close Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) = Numbr(DB('Admin - Department Control', !Department, 'Close Year')),
    IF(AttrN('t_Period', !t_Period, 'Index') > AttrN('t_Period',DB('Admin - Department Control', !Department, 'Close Month'), 'Index'),
      0,
      Continue),
    Continue));

#EndRegion



# Forecast is Actual until current month, then it takes the version specified in the model parameter cube
['Forecast','Current Year']=N:
  IF(AttrN('t_Period', !t_Period, 'Index')<=AttrN('t_Period', DB('Admin - Model Parameters', 'Actual Period', 'Value'), 'Index'),
    ['Actual'],
    DB('PL by Entity', DB('Admin - Model Parameters', 'Version for Forecast', 'Value'), !t_Period, !t_Year, !Entity, !Department, !Contract, !'Nominal Code' )
    );



# Bring values in from PL by Dept using the Nominal Mapping
# Also bring values from Subcontract Recharge using Nominal Lookup. 
[]=N: 
IF(DB('Admin - Nominal Mapping',!'Nominal Code', Attrs('Department', !Department, 'Mapping')) @<>'Standard',
  IF(!Entity@=DB('Admin - Nominal Mapping',!'Nominal Code', Attrs('Department', !Department, 'Mapping')),
    DB('PL by Dept', !Version, !t_Period, !t_Year, !Department, !Contract, !Nominal Code),
    0),
  IF(DB('Admin - Nominal Mapping',!'Nominal Code', Attrs('Department', !Department, 'Mapping')) @='Standard',
    IF(AttrS('Entity',!Entity, 'Company Code')@=ATTRS('Department', !Department, 'Company Code'),
      DB('PL by Dept', !Version, !t_Period, !t_Year, !Department, !Contract, !Nominal Code),
      0),
    0))

# All the credits
#Region Subcontract
+
IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'Care AEI', 'Cost' ),
  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'Care AEI', 'Credit')
,0)
+
IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'Care RPI', 'Cost' ),
  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'Care RPI', 'Credit')
,0)
+
# Managed is removed
#IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'Managed', 'Cost' ),
#  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'Managed', 'Credit')
#,0)
#-
#IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'Managed', 'Cost' ),
#  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'Managed', 'MS Credit')
#,0)
#-
IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'FM AEI', 'Cost' ),
  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'FM AEI', 'Credit')
,0)
+
IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'FM RPI', 'Cost' ),
  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'FM RPI', 'Credit')
,0)
# Then all the recharges
+
IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'Care AEI', 'Income' ),
  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'Care AEI', 'Recharge')
,0)
+
IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'Care RPI', 'Income' ),
  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'Care RPI', 'Recharge')
,0)
+
#IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'Managed', 'Income' ),
#  DB('Subcontract - Recharges', !Version, !t_Period, !Entity, !Department, !Contract, 'Managed', 'Recharge')
#,0)
#+
#IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'Managed', 'Income' ),
#  DB('Subcontract - Recharges', !Version, !t_Period, !Entity, !Department, !Contract, 'Managed', 'MS Recharge')
#,0)
#+
IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'FM AEI', 'Income' ),
  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'FM AEI', 'Recharge')
,0)
+
IF(!Nominal Code@=DB('Subcontract - Nominal Lookup', !Entity, !Contract, 'FM RPI', 'Income' ),
  DB('Subcontract - Recharges', !Version, !t_Period, !t_Year, !Entity, !Department, !Contract, 'FM RPI', 'Recharge')
,0)
#EndRegion
;



FEEDERS;

['Actual','Current Year']              =>['Forecast'];
['Board Version','Current Year']       =>['Forecast'];
['Operational  Budget','Current Year'] =>['Forecast'];
['Steady State','Current Year']        =>['Forecast'];

['All Departments', 'All Contracts']=>DB('Consolidation', !Version, !t_Period, !t_Year, !Entity, 'Source', !'Nominal Code' );

['Actual', 'All Entities'] => DB('PL by Dept', !Version, !t_Period, !t_Year, !Department, !Contract, !'Nominal Code' );

[] => DB('Cashflow', !Version, !t_Period, !t_Year, !Entity, !Department, 'Source', !Nominal Code);

['R228']=>DB('Cashflow - Non PL Cash Items', !Version, !t_Period, !t_Year, !Entity, 'LC Spend');