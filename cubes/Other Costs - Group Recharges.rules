  # --------------------------------------------------------------------------------------------------
  # Other Costs - Group Recharges
  # Pulls together individual costs from Other Costs By Dept cube for Group Recharges
  #
  # Paul De Duonni
  # Last updated 22/12/23
  # --------------------------------------------------------------------------------------------------
  
  Skipcheck;
  Feedstrings;
    
    # Get the individual costs which form the Group Recharges  
    ['Annual Cost Assumptions','R203']=N:
      IF(ATTRS('Department', !Department, 'Group Recharges')@='Y',
          DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department, !'Nominal Code' ),
      CONTINUE);   
  
    ['Annual Cost Assumptions','R290']=N:
      IF(ATTRS('Department', !Department, 'Group Recharges')@='Y',
          DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department, !'Nominal Code' ),
      CONTINUE);   
      
    ['Annual Cost Assumptions','S230']=N:
      IF(ATTRS('Department', !Department, 'Group Recharges')@='Y',
          DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department, !'Nominal Code' ),
      CONTINUE);   

    ['Annual Cost Assumptions','S258']=N:
      IF(ATTRS('Department', !Department, 'Group Recharges')@='Y',
          DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department, !'Nominal Code' ),
      CONTINUE);
    
    
    #NEW LSC / INTERNAL MANAGEMENT RULE 
    ['Annual Cost Assumptions','R101']=N:
      IF(ATTRS('Department', !Department, 'Group Recharges')@='Y',
          DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department, !'Nominal Code' ),
      CONTINUE);   

    ['Annual Cost Assumptions','R295']=N:
      IF(ATTRS('Department', !Department, 'Group Recharges')@='Y',
          DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department, !'Nominal Code' ),
      CONTINUE);  
  

    # Map IRPC & Staff Recruitment to NH44 
    ['NH44 Recharges','None','R203']=
      IF(ATTRS('Department', !Department, 'NH44 / NH49 Recharges')@='Y',
        ['Annual Cost Assumptions','R203'], 
      CONTINUE);
      
    ['NH44 Recharges','None','S230']=
      IF(ATTRS('Department', !Department, 'NH44 / NH49 Recharges')@='Y',
        ['Annual Cost Assumptions','S230'], 
      CONTINUE);
        
    ['NH44 Recharges','None','NH44','P462']=
      ['NH44 Recharges','All Departments','R203'];
      
    ['NH44 Recharges','None','NH44','P530']=
      ['NH44 Recharges','All Departments','S230'] - ['NH44 Recharges','NH44','S230'];

    ['NH44 Recharges','NH44','None','R299']=
      -['R299 (Head Office)','All Departments','R203'] - ['R299 (Head Office)','All Departments','S230']; 
      
      
    # Map Training to NH49
    ['NH49 Recharges','None','S258']=
      IF(ATTRS('Department', !Department, 'NH44 / NH49 Recharges')@='Y',
        ['Annual Cost Assumptions','S258'], 
      CONTINUE);

    ['NH49 Recharges','NH49','None','P574']=
      ['NH49 Recharges','All Departments','S258']; 
      
    ['NH49 Recharges','NH49','None','R299']=
      -['R299 (Head Office)','All Departments','S258'];     



    # Map IRPC, Training & Recruitment to R299
    ['R299 (Services)','None','R299']=
    #  IF(ATTRS('Department', !Department, 'NH44 / NH49 Recharges')@='Y',
        ['Annual Cost Assumptions','R203'] + ['Annual Cost Assumptions','S230'] + ['Annual Cost Assumptions','S258'];
    #  CONTINUE);

    ['R299 (Head Office)']=
      IF(ATTRS('Department', !Department, 'R299 Recharges')@='Y',
        ['Annual Cost Assumptions'], 
      CONTINUE);
      
      
    # Map recharges to Admin Depts
    ['R285 & R299 (Admin Depts)','R299']=
      IF(!Admin Department@=ATTRS('Department', !Department, 'Admin Dept') & !Admin Department@<>'NH45',
       -( ['Annual Cost Assumptions','R203'] + ['Annual Cost Assumptions','S230'] + ['Annual Cost Assumptions','S258']),
      CONTINUE);
      
    ['R285 & R299 (Admin Depts)','R285']=
      IF(!Admin Department@=ATTRS('Department', !Department, 'Admin Dept') & !Admin Department@<>'NH45',
        (['Annual Cost Assumptions','R203'] + ['Annual Cost Assumptions','S230'] + ['Annual Cost Assumptions','S258']),
      CONTINUE);
 

    # Map Management Fee income to GP01
    
     ['GP01 Recharges','None', 'R290']=
      IF(ATTRS('Department', !Department, 'GP01 Mgt Fee Inc')@='Y',
         ['Annual Cost Assumptions','R290'],
      CONTINUE);
     
     ['GP01 Recharges','GP01','None','P404']=
      ['GP01 Recharges','Department':'CH01','R290']; 
     
     ['GP01 Recharges','GP01','None','P407']=
      ['GP01 Recharges','Department':'LH01','R290']; 
     
     ['GP01 Recharges','GP01','None','P409']=
      ['GP01 Recharges','Department':'HV01','R290']; 
      
     ['GP01 Recharges','GP01','None','P411']=
      ['GP01 Recharges','Department':'CK01','R290']; 
      

     #NEW LSC RULE  
      
     ['GP01 Recharges', 'None', 'GP01', 'R101']=
       -['Annual Cost Assumptions','None','All Departments', 'R101'];
 
     ['GP01 Recharges', 'None', 'GP01', 'R295']=
       -['Annual Cost Assumptions','None','All Departments', 'R295'];
     
      
FEEDERS;

#Internal Feeders


# Map IRPC & Staff Recruitment to NH44 

['Annual Cost Assumptions','R203','None']=> ['NH44 Recharges','R203'];
['Annual Cost Assumptions','S230','None']=> ['NH44 Recharges','S230'];

['NH44 Recharges','R203','None']=>['NH44','P462'];      

['NH44 Recharges','S230','None']=>['NH44','P530'];

['R299 (Head Office)','R203','None']=>['NH44 Recharges','NH44','R299'];
['R299 (Head Office)','S230','None']=>['NH44 Recharges','NH44','R299'];


# Map Training to NH49

['Annual Cost Assumptions','S258','None']=>['NH49 Recharges'];

['NH49 Recharges','S258','None']=> ['NH49 Recharges','NH49','P574'];

['R299 (Head Office)','S258','None']=> ['NH49 Recharges','NH49','R299'];


# Map IRPC, Training & Recruitment to R299

['Annual Cost Assumptions','R203','None'] =>['R299 (Services)','R299'];

['Annual Cost Assumptions','S230','None'] =>['R299 (Services)','R299'];

['Annual Cost Assumptions','S258','None'] =>['R299 (Services)','R299'];

['Annual Cost Assumptions']  =>  ['R299 (Head Office)'];


# Map recharges to Admin Depts

['Annual Cost Assumptions','R203'] =>['R285 & R299 (Admin Depts)','R299'];

['Annual Cost Assumptions','S230'] =>['R285 & R299 (Admin Depts)','R299'];

['Annual Cost Assumptions','S258'] =>['R285 & R299 (Admin Depts)','R299'];

['Annual Cost Assumptions','R203'] =>['R285 & R299 (Admin Depts)','R285'];

['Annual Cost Assumptions','S230'] =>['R285 & R299 (Admin Depts)','R285'];

['Annual Cost Assumptions','S258'] =>['R285 & R299 (Admin Depts)','R285'];
      

# Map Management Fee income to GP01

['Annual Cost Assumptions','R101','None']=>['GP01 Recharges','R101'];

['Annual Cost Assumptions','R290','None']=>['GP01 Recharges','R290'];

['Annual Cost Assumptions','R295','None']=>['GP01 Recharges','R295'];

['GP01 Recharges','Department':'CH01','R290','None']=>['GP01 Recharges','GP01','P404'];

['GP01 Recharges','Department':'LH01','R290','None']=>['GP01 Recharges','GP01','P407'];

['GP01 Recharges','Department':'HV01','R290','None']=>['GP01 Recharges','GP01','P409'];

['GP01 Recharges','Department':'CK01','R290','None']=>['GP01 Recharges','GP01','P411'];


#External Feeders

['R285 & R299 (Admin Depts)']=>DB('PL by Dept', !Version, !t_Period, !t_Year, !Admin Department, AttrS('Department', !Admin Department, 'Contract') , !'Nominal Code'); 

['GP01 Recharges']=> DB('PL by Dept', !Version, !t_Period, !t_Year, 'GP01', AttrS('Department', 'GP01', 'Contract'), !'Nominal Code' );

['NH44 Recharges']=> DB('PL by Dept', !Version, !t_Period, !t_Year, 'NH44', AttrS('Department', 'NH44', 'Contract'), !'Nominal Code' );

['NH49 Recharges']=> DB('PL by Dept', !Version, !t_Period, !t_Year, 'NH49', AttrS('Department', 'NH49', 'Contract'), !'Nominal Code' );

['R299 (Services)']=>DB('PL by Dept', !Version, !t_Period, !t_Year, !Department, AttrS('Department', !Department, 'Contract') , !'Nominal Code'); 
