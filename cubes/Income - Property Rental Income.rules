  # --------------------------------------------------------------------------------------------------
  # Property Rental Income
  # Calculates monthly income from rental properties incl utilities recharges and service charges
  #
  # Created by Jenny Hassett 
  # --------------------------------------------------------------------------------------------------

SKIPCHECK;
FEEDSTRINGS;

#Get number of properties from 'Bed Numbers' cube
#For CK40/CK41/CK43/GP13 (Service Charges) need to use CK10/CK11/CK13/GP12 bed numbers
['Number of Properties']=N:
If((!Department)@='CK40',
DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, 'CK10', !Property Type, !Funding Types - Beds),
CONTINUE);

['Number of Properties']=N:
If((!Department)@='CK41',
DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, 'CK11', !Property Type, !Funding Types - Beds),
CONTINUE);

['Number of Properties']=N:
If((!Department)@='CK43',
DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, 'CK13', !Property Type, !Funding Types - Beds ),
CONTINUE);

['Number of Properties']=N:
If((!Department)@='GP13',
DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, 'GP12', !Property Type, !Funding Types - Beds ),
CONTINUE);

['Number of Properties']=N:
DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, !Property Type, !Funding Types - Beds);
   
#Average number of beds (all bed types) for full year
['FY', 'Number of Properties']=C:
(['April']+['May']+['June']+['July']+['August']+['September']+['October']+['November']+['December']+['January']+['February']+['March'])/12;

#Number of Properties for all Rental Income Types = look at Number of Properties Rental Income
['All Rental Income Elements', 'Number of Properties']=C:
['Rental Income', 'Number of Properties'];

['Number of Properties', 'All Property Types']=C:
['1 Bed Flat']+['2 Bed Flat']+['NCB Property']+['Other'];


#Calculate Monthly Income
['Monthly Income']=N:
  ((['Current Year Rate (weekly)']*(['Number of Properties']*52)/12)*(1-['Void % Utility Income only'])+
  (['Current Year Rate (yearly)']*(['Number of Properties']/12))*(1-['Void % Utility Income only'])+
  (['Current Year Rate (monthly)']*['Number of Properties'])*(1-['Void % Utility Income only']));

#['Monthly Income', 'FY']=C: 
#['April']+['May']+['June']+['July']+['August']+['September']+['October']+['November']+['December']+['January']+['February']+['March'];
  
#Void % is manual input
['Void %','All Rental Income Elements']=C:
['Void %', 'Rental Income'];

['Void %', 'FY']=C: 
(['April']+['May']+['June']+['July']+['August']+['September']+['October']+['November']+['December']+['January']+['February']+['March'])/12;

['Void % Utility Income only', 'FY']=C: 
(['April']+['May']+['June']+['July']+['August']+['September']+['October']+['November']+['December']+['January']+['February']+['March'])/12;

#Calculate Monthly Void Value (affecting all rental income elements)
['Monthly Void Value £']=N:
  ['Monthly Income']*['Void %'];

#Bad Debt % is manual input
['Bad Debt %','FY']=C: 
(['April']+['May']+['June']+['July']+['August']+['September']+['October']+['November']+['December']+['January']+['February']+['March'])/12;

#Calculate Monthly Bad Debt Value
['Monthly Bad Debt £']=N:
  ['Monthly Income']*['Bad Debt %'];


FEEDERS;
#Internal Feeders
['Number of Properties']=>['Monthly Income'];
['Current Year Rate (monthly)']=>['Monthly Income'];
['Monthly Income'] => ['Monthly Void Value £'];
['Monthly Income'] => ['Monthly Bad Debt £'];


# External Feeders 
# Added by Cathy 30/10/2023
['Net Monthly Rental Income']=>DB('Income - Property Rental Income - Summary', !Version, !t_Year, !t_Period, !Department, !'Property Rental Income Element', ATTRS( 'Property Rental Income Element', !Property Rental Income Element, 'Nominal Code'));

#Added by Helen 20/12/2023
['Monthly Income'] => DB('Income - Property Rental Income - Summary', !Version, !t_Year, !t_Period, !Department, !Property Rental Income Element, ATTRS( 'Property Rental Income Element', !Property Rental Income Element, 'Nominal Code'));

['Monthly Bad Debt £'] => DB('Income - Property Rental Income - Summary', !Version, !t_Year, !t_Period, !Department, !Property Rental Income Element, ATTRS( 'm_Property Rental Income Calculation', !m_Property Rental Income Calculation, 'Nominal Code'));

['Monthly Void Value £'] => DB('Income - Property Rental Income - Summary', !Version, !t_Year, !t_Period, !Department, !Property Rental Income Element, ATTRS( 'm_Property Rental Income Calculation', !m_Property Rental Income Calculation, 'Nominal Code'));