  # --------------------------------------------------------------------------------------------------
  # Other Costs - Resident & Staff Related & Other Costs
  # Calculates resident related operating costs using Other Costs - Annual Cost Assumptions 
  #
  # Paul De Duonni
  # Last updated 06/12/23
  # --------------------------------------------------------------------------------------------------
  
  Skipcheck;
  Feedstrings;
  
  #Average totals for full year where required
  ['FY']=C:
  IF(ATTRS('m_Resident Related Costs Calculation', !m_Resident Related Costs Calculation, 'Average')@='Y',
    (['April']+['May']+['June']+['July']+['August']+['September']+['October']+['November']+['December']+['January']+['February']+['March'])/12,
  CONTINUE);
  
  
  #Zero totals at consolidated level if not required
  ['All Other Costs']=C:
  IF(ATTRS('m_Resident Related Costs Calculation', !m_Resident Related Costs Calculation, 'Zero Consolidated')@='Y',
    0,
  CONTINUE);
  
  ['All Departments']=C:
  IF(ATTRS('m_Resident Related Costs Calculation', !m_Resident Related Costs Calculation, 'Zero Consolidated')@='Y',
    0,
  CONTINUE);
  
  
  # Get the number of beds and day care places from the Income - Bed Numbers cube
  ['Number of Beds']=
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, 'All Care Bed Types', 'Total Funding Types'  ) - ['Number of Day Care Places'];

  ['Number of Day Care Places']=
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, 'Older Person Day Care', 'Total Funding Types'  )+
      DB('Income - Bed Numbers', !Version, !t_Year, !t_Period, !Department, 'Dementia Day Care', 'Total Funding Types'  );


  #Get the WTE's from Payroll - Establishments cube
  ['Number of WTEs']=
    IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Cost Type / Frequency' )@='Per WTE Per Annum',
      DB('Payroll - Establishments', !Version, !t_Period, !t_Year, !Department,'All Staff Grades', 'Hours' ) \ 37.5,
    CONTINUE);
  

  # Get the number of days in the period
  # If year is a leap year get 'Number of days leap'  
  ['Number of Days Per Period']=
      IF(MOD(NUMBR(ATTRS( 't_Year', !t_Year, 'Financial Year' ))+1,4)=0,
           ATTRN( 't_Period', !t_Period, 'Number of Days Leap' ),
           CONTINUE);
   
  # Otherwise just get 'Number of days'
  ['Number of Days Per Period']=N:   
      ATTRN( 't_Period', !t_Period, 'Number of Days' );
  
  
  # Get the cost assumption amounts from the Other Costs - Annual Cost Assumptions cube 
  ['Cost (Per Bed Per Week)']=N:
  IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Cost Type / Frequency' )@='Per Resident Per Week',
      IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount' )=0,
         IF(['Number of Beds']<=10,
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (1 - 10 Beds)' ),
         IF(['Number of Beds']<=24,
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (11 - 24 Beds)' ),
         IF(['Number of Beds']<=39,  
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (25 - 39 Beds)' ),
         IF(['Number of Beds']<=59,
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (40 - 59 Beds)' ),
         IF(['Number of Beds']>=60,
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (60+ Beds)' ), 
         0))))),
       DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount')),
  CONTINUE);
  
  ['Cost (Per Bed Per Annum)']=N:
  IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Cost Type / Frequency' )@='Per Resident Per Annum',
      IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount' )=0,
         IF(['Number of Beds']<=10,
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (1 - 10 Beds)' ),
         IF(['Number of Beds']<=24,
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (11 - 24 Beds)' ),
         IF(['Number of Beds']<=39,  
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (25 - 39 Beds)' ),
         IF(['Number of Beds']<=59,
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (40 - 59 Beds)' ),
         IF(['Number of Beds']>=60,
           DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (60+ Beds)' ), 
         0))))),
       DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount')),
  CONTINUE);
   
  ['Cost (Per Bed Per Annum)']=N:
  IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Cost Type / Frequency' )@='Per Resident Per Annum',
     DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount' ),
  CONTINUE);
  
  ['Cost (Per Day Care Place Per Day)']=N:
     DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Variable Amount (Day Care Places)' );

  ['Cost (Per WTE Per Annum)']=N:
    IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Cost Type / Frequency' )@='Per WTE Per Annum',
      DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount' ),
    CONTINUE);

  ['Cost (Per Home / Dept Per Month)']=N:
    IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Cost Type / Frequency' )@='Per Home / Dept Per Month',
      DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount' ),
    CONTINUE);

  ['Cost (Per Home / Dept Per Quarter)']=N:
    IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Cost Type / Frequency' )@='Per Home / Dept Per Quarter',
      DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount' ),
    CONTINUE);

  ['Cost (Per Home / Dept Per Annum)']=N:
    IF(DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Cost Type / Frequency' )@='Per Home / Dept Per Annum',
      DB('Other Costs - Annual Cost Assumptions', !Version, !t_Year, !Department, !'Other Costs', 'Fixed Amount' ),
    CONTINUE);


  # Calculate the resulting costs per home / dept
   ['Total Costs']=N:
       ['Cost (Per Bed Per Week)']*['Number of Beds']*52\12*(1-['Void % Adjustment'])
      +['Cost (Per Bed Per Annum)']*['Number of Beds']\12*(1-['Void % Adjustment'])
      +['Cost (Per WTE Per Annum)']*['Number of WTEs']\12
      +['Cost (Per Home / Dept Per Month)']
      +['Cost (Per Home / Dept Per Quarter)']*4\12
      +['Cost (Per Home / Dept Per Annum)']\12;
  
   # Calculate the additional day care places costs
   ['Additional Costs (Day Care Places)']=N:
     ['Cost (Per Day Care Place Per Day)'] * ['Number of Day Care Places']*['Number of Day Care Days Per Week']*52\12;


   #Calculate phasing adjustment
   ['Phasing Adjustment Amount']=N:
      ['Monthly Costs Per Home / Dept']*['Phasing Adjustment %'];
    
   ['Adjusted Total With Rounding Errors']=N:
     ['Monthly Costs Per Home / Dept'] + (['Monthly Costs Per Home / Dept'] * ['Phasing Adjustment %']);
  
   #Calculate overall total budgeted cost per home
   ['Budgeted Costs Per Home / Dept']=N:
     ['Adjusted Total With Rounding Errors'] * (['Monthly Costs Per Home / Dept', 'FY'] \ ['Adjusted Total With Rounding Errors', 'FY']);  
  
    
FEEDERS;   
    
#Internal Feeders
['Cost (Per Bed Per Week)']=>
  ['Monthly Costs Per Home / Dept'],
  ['Phasing Adjustment Amount'], 
  ['Adjusted Total With Rounding Errors'],
  ['Number of Days Per Period'];

['Cost (Per Bed Per Annum)']=>
  ['Monthly Costs Per Home / Dept'],
    ['Phasing Adjustment Amount'], 
    ['Adjusted Total With Rounding Errors'];

['Cost (Per WTE Per Annum)']=>
  ['Monthly Costs Per Home / Dept'],
  ['Phasing Adjustment Amount'], 
  ['Adjusted Total With Rounding Errors'];

['Cost (Per Home / Dept Per Month)']=>
  ['Monthly Costs Per Home / Dept'],
  ['Phasing Adjustment Amount'], 
  ['Adjusted Total With Rounding Errors'];

['Cost (Per Home / Dept Per Quarter)']=>
  ['Monthly Costs Per Home / Dept'],
  ['Phasing Adjustment Amount'], 
  ['Adjusted Total With Rounding Errors'];

['Cost (Per Home / Dept Per Annum)']=>
  ['Monthly Costs Per Home / Dept'],
  ['Phasing Adjustment Amount'], 
  ['Adjusted Total With Rounding Errors'];

['Cost (Per Day Care Place Per Day)']=>
  ['Monthly Costs Per Home / Dept'],
  ['Additional Costs (Day Care Places)'], 
  ['Phasing Adjustment Amount'], 
  ['Adjusted Total With Rounding Errors'];

['Additional Costs (Day Care Places)']=>['Adjusted Total With Rounding Errors'];
['Adjustment']=>['Adjusted Total With Rounding Errors'];

['Monthly Costs Per Home / Dept']=>['Budgeted Costs Per Home / Dept'];


#External Feeders  
['Budgeted Costs Per Home / Dept']=>DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department,'Nominal Code':'Operating Costs');
['Budgeted Costs Per Home / Dept']=>DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department,'Nominal Code':'Overheads');
['Budgeted Costs Per Home / Dept']=>DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department,'Nominal Code':'Depreciation and amortisation');
['Budgeted Costs Per Home / Dept']=>DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department,'Nominal Code':'Exceptional Items');
['Budgeted Costs Per Home / Dept']=>DB('Other Costs - Costs by Dept', !Version, !t_Period, !t_Year, !Department,'Nominal Code':'Other Interest Payable');