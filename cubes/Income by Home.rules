# --------------------------------------------------------------------------------------------------
# Income by Home
# Created by Cathy # Last updated 08/12/2023
# --------------------------------------------------------------------------------------------------

SKIPCHECK;
FEEDSTRINGS;


#Region Opening and Closing Months

[]=N:
IF(DB('Admin - Department Control', !Department, 'Open Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) < Numbr(DB('Admin - Department Control', !Department, 'Open Year')),
    0,
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Open Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) = Numbr(DB('Admin - Department Control', !Department, 'Open Year')),
    IF(AttrN('t_Period', !t_Period, 'Index') < AttrN('t_Period',DB('Admin - Department Control', !Department, 'Open Month'), 'Index'),
      0,
      Continue),
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Close Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) > Numbr(DB('Admin - Department Control', !Department, 'Close Year')),
    0,
    Continue));

[]=N:
IF(DB('Admin - Department Control', !Department, 'Close Year')@='',
  Continue,
  IF(Numbr(Attrs('t_Year', !t_Year, 'Financial Year')) = Numbr(DB('Admin - Department Control', !Department, 'Close Year')),
    IF(AttrN('t_Period', !t_Period, 'Index') > AttrN('t_Period',DB('Admin - Department Control', !Department, 'Close Month'), 'Index'),
      0,
      Continue),
    Continue));

#EndRegion



#Region Bring values from other income cubes. 

['m_Income by Home':'Other Income']=N:
DB('Income - Other Income - Summary', !Version, !t_Year, !t_Period, !Department, 'All Other Income Items', !'Nominal Code', 'Amount Â£');

['Property Rental Income']=N:
DB('Income - Property Rental Income - Summary', !Version, !t_Year, !t_Period, !Department, 'All Rental Income Elements', !'Nominal Code' );

['Resident Fees'] = N:
DB('Income - Resident Fees - Summary', !Version, !t_Year, !t_Period, !Department, 'Total Income', !Nominal Code);

['m_Income by Home':'Unitary Charge Income'] = N:
DB('Income - Unitary Charge Income - Summary', !Version, !t_Year, !t_Period, !Department, 'Total Contract Sum', !Nominal Code);

#EndRegion



FEEDERS;

# External Feeders
['Value']=>DB('PL by Dept', !Version, !t_Period, !t_Year, !Department, Attrs('Department', !Department, 'Contract'), !'Nominal Code' );

['m_Income by Home':'Value','P111']=>DB('Subcontract - Other In Year Charges', !Version, !t_Period, !t_Year, !Department, 'De Montfort', 'Care AEI', 'Value' );

['m_Income by Home':'Value','P123', 'BA07']=>DB('Subcontract - Other In Year Charges', !Version, !t_Period, !t_Year, !Department, 'Barton', 'FM AEI', 'Value' );

['m_Income by Home':'Value','P123', 'HH12']=>DB('Subcontract - Other In Year Charges', !Version, !t_Period, !t_Year, !Department, 'Herefordshire', 'FM AEI', 'Value' );

['m_Income by Home':'Value','P115', 'HH03']=>DB('Subcontract - Other In Year Charges', !Version, !t_Period, !t_Year, !Department, 'Herefordshire', 'Care RPI', 'Value' );