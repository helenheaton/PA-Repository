SKIPCHECK;
FEEDSTRINGS;

#Region April

# Override is a true override

['April Override Value']=N:
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='Override',
  stet,  
  0);

['Hourly Pay - April']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='Override',
    ['April Override Value'],
  Continue);

# Monetary Increase

['April Monetary Increase']=N:
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='Monetary Amount',
  stet, 
  0);

['Hourly Pay - April']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='Monetary Amount',
  ['Current Hourly Rate']+['April Monetary Increase'],
  Continue);

# Percentage Increase

['April Increase Percentage']=N:
  DB('Payroll - Assumptions', !Version, !t_Year, 'Care Staff April Increase' );

['April Increase Value']=N:
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='Percentage Increase',
  ['Current Hourly Rate'] * DB('Payroll - Assumptions', !Version, !t_Year, 'Care Staff April Increase' ),
  0);

['Hourly Pay - April']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='Percentage Increase',
  ['Current Hourly Rate']+['April Increase Value'],
  Continue);

# Living Wage Increase

['Living Wage Increase Value']=N:
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='Living Wage Increase',
  DB('Payroll - Assumptions', !Version, !t_Year, 'Living Wage Increase on Prior Year' ),
  0);
  
['Hourly Pay - April']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='Living Wage Increase',
  ['Current Hourly Rate']+['Living Wage Increase Value'],
  Continue);

# If Blank

['Hourly Pay - April']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - April')@='',
  ['Current Hourly Rate'],
  Continue);

#EndRegion



#Region October

# Override is a true override

['October Override Value']=N:
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - October')@='Override',
  stet,  
  0);

['Hourly Pay - October']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - October')@='Override',
    ['October Override Value'],
  Continue);

# Monetary Increase

['October Monetary Increase']=N:
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - October')@='Monetary Amount',
  stet, 
  0);

['Hourly Pay - October']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - October')@='Monetary Amount',
  ['Hourly Pay - April']+['October Monetary Increase'],
  Continue);

# Percentage Increase

['October Increase Percentage']=N:
  DB('Payroll - Assumptions', !Version, !t_Year, 'Care Staff October Increase' );

['October Increase Value']=N:
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - October')@='Percentage Increase',
  ['Hourly Pay - April'] * DB('Payroll - Assumptions', !Version, !t_Year, 'Care Staff October Increase' ),
  0);

['Hourly Pay - October']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - October')@='Percentage Increase',
  ['Hourly Pay - April']+['October Increase Value'],
  Continue);
  
# If blank

['Hourly Pay - October']=
IF(DB('Payroll - Pay Rates', !Version, !t_Year, !Department, !'Staff Grade', 'Pay Increase Method - October')@='',
  ['Hourly Pay - April'],
  Continue);

#EndRegion


FEEDERS;

# Can't feed from the assumptions cube so these are fed internally

['Current Hourly Rate']=>['April Increase Percentage'];

['Current Hourly Rate']=>['Living Wage Increase Value'];

['Current Hourly Rate']=>['October Increase Percentage'];


#Region April

['April Override Value']=>['Hourly Pay - April'];

['Current Hourly Rate']=>['Hourly Pay - April'];

['April Monetary Increase']=>['Hourly Pay - April'];

['Current Hourly Rate']=>['April Increase Value'];

['April Increase Value']=>['Hourly Pay - April'];

['Living Wage Increase Value']=>['Hourly Pay - April'];

#EndRegion


#Region October

['October Override Value']=>['Hourly Pay - October'];

['Hourly Pay - April']=>['Hourly Pay - October'];

['October Monetary Increase']=>['Hourly Pay - October'];

['Hourly Pay - April']=>['October Increase Value'];

['October Increase Value']=>['Hourly Pay - October'];

#EndRegion


# External Feeder

['Hourly Pay - April']=>DB('Payroll - Homes Pay Calc inc Vacancies', !Version, 'H1', !t_Year, !Department, !'Staff Grade', 'Vacancy Hours Pay Rate' ); 


['Hourly Pay - October']=>DB('Payroll - Homes Pay Calc inc Vacancies', !Version, 'H2', !t_Year, !Department, !'Staff Grade', 'Vacancy Hours Pay Rate' ); 

